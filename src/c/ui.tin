#screen get cols {config[cols]}
#screen get rows {config[rows]}
#var messages {}
#event {SCREEN RESIZE} {#var config[rows] %0;#var config[cols] %1;ui r}
#alias {ui} {
	#if {"%1" != "r"} {
		#math config[ui] {($config[ui] + 1) % 2};
	};
	#if {$config[ui] == 0} {
		#split;
		map;
	};
	#else {
		#math config[panel_left] {$config[game_cols] - $config[cols] + 1}; 
		#math config[map_rows] {($config[rows] - 4) / 3};
		#split 0 3 0  {$config[cols] - $config[game_cols]};
		map 1;
		ui_show_messages;
	}
}
#alias {ui.send}
{
	#if {"%0" == ""} {#return};
	#echo {%0};
	ui_message %0;
}
#alias {ui_message} {
	#if {"%0" == ""} {#return};
	#list messages add {%0};
	ui_show_messages;
}
#alias {ui_show_messages}{
	#local rows 0;
	#math rows {$config[rows]-$config[map_rows]-8};
	#screen clear square {$config[map_rows]+3} {$config[panel_left]} {$config[map_rows]+5+$rows} {-1};
	#draw cyan rounded box {$config[map_rows]+3} {$config[panel_left]} {$config[map_rows]+5+$rows} {-1};
	#if {&messages[] == 0} {#return};
	#math count {&messages[] > $rows ? $rows : &messages[]};
	#loop {-$count} {-1} {i} {#line ignore {#show {$messages[$i]} {$config[map_rows]+4+$count+$i} {$config[panel_left]+2}}};
}
#tick {ui_draw} {
	#if {$config[ui] != 1} {#return};
	#screen clear square {-3} {2} {-3} {$config[cols] - 3};
	#show {@hp_draw{}} {-3} {2};

	#screen clear square {$config[map_rows]} {$config[panel_left]} {$config[map_rows]+2} {-1};
	#show {@map_draw{}} {$config[map_rows]} {$config[panel_left]};
	#show {@hp_ex{}} {$config[map_rows] + 1} {$config[panel_left]};
	#show {@hp_dh{}} {$config[map_rows] + 2} {$config[panel_left]};
} {0.5}

#macro {\cb} {#buffer up {$config[cols] - 3}}
#macro {\cf} {#buffer down {$config[cols] - 3}}
#macro {\cy} {#buffer up {1}}
#macro {\ce} {#buffer down {1}}
