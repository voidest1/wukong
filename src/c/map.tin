#var inroom {}
#var map {{running}{0}{rooms}{}{after}{}{mode}{0}}

#alias {g}
{
	#if {&{walk} != 0}
    {
        #echo {You are in running already};
        #return;
    };
    wk_require walk;
    walk.start roomname {%1};
}
#alias {scout}
{
	#if {"%2" == ""} 
	{
		#echo {Syntax: scout <keyword> <area> [<reciever>]};
		#return;
	};
	#if {&{walk} != 0} 
	{
		#echo {You are in running already};
		#return
	};
	wk_require walk;
	#var walk[mode] 1;
	#var walk[keyword] {%1};
	#if {"%3" != ""}
	{
		#list walk[recievers] add {%3};
	};
	walk.start roomarea %2;
}
#alias {ms}
{
	#local pos {%1};
	#if {"%1" == ""} 
	{
		#local pos $inroom[game][name];
		#local rooms {};
		#local name $inroom[game][name];
		#map list {roomname} {$name} {variable} {rooms};
		#if {&rooms[] == 0}
		{
			#echo {Cannot find this room by name $inroom[game][name] on map};
			#bell;
			#return;
		};
		#if {&rooms[] > 1}
		{
			#map list {roomname} {$name} {roomarea} {$inroom[map][area]} {variable} {rooms};
			#if {&rooms[] != 1}
			{
				#map list {roomname} {$name} {roomexits} {$inroom[game][exits]} {variable} {rooms};
				#if {&rooms[] > 0}
				{
					#local pos *rooms[+1];
				};
			};
			#else
			{
				#local pos *rooms[+1];
			};
		};
		#else
		{
			#local pos *rooms[+1];
		}
	};
	#map goto $pos;
}
#alias {map.maze}
{
	wk_require maze;
	maze.run %0;
}
#act {^你的动作还没有完成，不能移动。$} {#map undo}
#nop look输出的房间名称，注意：因为这是第一行事件，想在内容显示完毕再做的指令，需要延迟0.1s;
#act {^%P%S%P - %P$}
{
    #var inroom[game][name] %2;
    #if {"%2" == "荡悠悠三更梦"} {ms dream};
    #if {$edit[name] != 1} {#return};
    #if {"$inroom[game][name]" != "$inroom[map][name]"} 
    {
    	#map set roomname $inroom[game][name];
    	ui_message {#$inroom[map][vnum] Changed $inroom[map][name] to $inroom[game][name]};
    	#var inroom[map][name] $inroom[game][name];
    	#bell;
    };
}
#act {^%s这里%2的出口是%s%4。$} {
	#local s {%4};
	#local r {{、}{;}{和}{;}{ }{}{down}{d}{east}{e}{north}{n}{south}{s}{west}{w}{up}{u}};
	#foreach {*r[%*]} {f} {#replace s {$f} {$r[$f]}};
	#var inroom[game][exits] $s;
	#if {$init == 1} {ui;ms;#echo {---- Welcome to 悟空v0.0.1 for 西游记(MUD) ----};#unvar init};
}

#event {MAP ENTER ROOM}
{
    #map get all {inroom[map]} {%0};
}

#act {^护宫卫士大喝一声：下去！你被扔回了下界。$} {ms 96;#bell}
#alias {map} {
	#if {"%1" == ""} {
		#line quiet {#map destroy world};
		#echo {Swith off to map};
		#return;
	};
	#line quiet {#map destroy world};
	#map read {./map/xyj.map};
	#map goto 1;
	#map flag nofollow off;
	#map flag static on;
	#map offset 1 {$config[panel_left]} {$config[map_rows] - 1} {-1};
	#if {$config[ui] != 1} {#map flag vtmap};
	#echo {Switch on to map};
}
#alias {map_style} {#map flag asciigraphics}
#func {map_draw} {
	#local __color {<099>};
	#if {"$inroom[game][name]" != "$inroom[map][name]"} {
		#local __color {<139>};
	};
	#format {result} {%s - %s%s<099> #%s@%s} {$inroom[game][name]} {$__color} {$inroom[map][name]} {$inroom[map][vnum]} {$inroom[map][area]}
}

#func {map_area_c2e} {
	#switch {"%0"} {
		#case "长安城" {#return changan};
		#case "长安城西" {#return changan-west};
		#case "开封城" {#return kaifeng};
		#case "高老庄" {#return gao};
		#case "方寸山" {#return fangcun};
		#case "龙宫" {#return longgong};
		#case "普陀山" {#return putuo};
		#default {#return %0};
	}
}
#nop ####### Edit ######;
#alias {map.setarea}
{
	#if {"%0" == "" || ("%2" != "" && "%3" == "")}
	{
		#echo {Syntax: map.setarea <areaname> [<from vnum> <to vnum>]};
		#bell;
		#return;
	};
	#if {"%3" != ""}
	{
		#loop {%2} {%3} {n}
		{
			#map set {roomarea} {%1} {$n};
		};
		#echo {Setup area %1 from %2 to %3};
	};
	#else
	{
		#map set {roomarea} {%1};
		#echo {Setup area %1 to $inroom[map][name]};
		#var inroom[map][area] {%1};
	}
}
#alias {map.setname} {
	#map set {roomname} $inroom[game][name];
	#var inroom[map][name] $inroom[game][name];
	#bell;
}
#var edit {{name}{0}}
#alias {map.new} {#map dig %0 new}
#alias {map.autoname} {#math edit[name] {($edit[name] + 1) % 2};#local s {ON};#if {$edit[name] == 0} {#local s {OFF}};ui_message {Switch auto-name to $s};#bell};

#alias {map.save} {
	#map write {./map/xyj.map};
	#echo {Saved map to ./map/xyj.map};
}
#alias {map.check} {
    #map list {variable} {__rooms};
    #local {__names} {};
    #local cnt 0;
	#local __id 0;
	#foreach {*__rooms[%*]} {__id} {
		#map get roomname {__name} {$__id};
		#if {"$__name" == ""} {#echo {#$__id was empty name};#continue};
		#if {$__names[$__name][count] >= 1} {
            #if {$__names[$__name][count] == 1} {#map set roomname {$__name$__names[$__name][count]} {$__names[$__name][id]};#math cnt {$cnt+1}};
            #math {__names[$__name][count]} {$__names[$__name][count]+1};
            #map set roomname {$__name$__names[$__name][count]} {$__id};
            #math cnt {$cnt+1};
        };
		#else {
            #local __names[$__name] {{id}{$__id}{count}{1}};
		}
	};
	#unvar __rooms;
	#unvar __name;
    #echo {$cnt duplicated names has been changed.};
}
#alias fly {
	#send {fly %0};
	#act {^到了！你按下云头跳了下来。$} {ms %0;#unact {^到了！你按下云头跳了下来。$}};
	#act {^咦？．．．怎么还在原来的地方？$} {#map undo;#unact {^到了！你按下云头跳了下来。$}};
	#delay {3} {#unact {^到了！你按下云头跳了下来。$}};
}
#act {^警幻仙姑说道：又是一根木头...好，你这就去吧。$} {ms $bed;#unvar bed}
#alias sleep {#var bed $inroom[map][vnum];#send sleep %0}
