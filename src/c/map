#var inroom {}
#act {^%P%S%P - %P$} {
	#var inroom[cname] %2;
	#if {$scout[run] == 1} {#math scout[gstep] {$scout[gstep] + 1};#if {$scout[gstep] == $scout[mstep] && $scout[gstep] == $scout[rstep]} {#delay {0.1} {#math scout[mstep] {$scout[mstep] + 1};#path walk forward}}};
	#if {$init == 1} {ui;map_pos %2;#echo {---- Welcome to 悟空v0.0.1 for 西游记(MUD) ----};#unvar init};
	#if {"%2" == "荡悠悠三更梦"} {map_pos dream};
	#if {$edit[name] != 1} {#return};
	#if {"$inroom[cname]" != "$inroom[map][name]"} {#map set roomname $inroom[cname];ui_message {#$inroom[map][vnum] Changed $inroom[map][name] to $inroom[cname]};#var inroom[map][name] $inroom[cname];#bell;};
}
#event {MAP ENTER ROOM} {
	#if {$scout[run] == 1} {#math scout[rstep] {$scout[rstep] + 1}};
	#map get all {inroom[map]} {%0};
}
#act {^%1一把抓住了你！$} {#line quiet {#map undo;scout.stop;#bell}}
#act {^但没多会儿就被憋得半死，只好赶紧爬上岸来。$} {#line quiet {#map undo;#path stop;#bell}}
#act {^护宫卫士大喝一声：下去！你被扔回了下界。$} {map_pos 96;#bell}
#alias {map} {
	#if {"%1" == ""} {
		#line quiet {#map destroy world};
		#echo {Swith off to map};
		#return;
	};
	#line quiet {#map destroy world};
	#map read {./map/xyj.map};
	#map goto 1;
	#map flag nofollow off;
	#map flag static on;
	#map offset 1 {$config[panel_left]} {$config[map_rows] - 1} {-1};
	#if {$config[ui] != 1} {#map flag vtmap};
	#echo {Switch on to map};
}
#alias {map_style} {#map flag asciigraphics}
#func {map_draw} {
	#local __color {<099>};
	#if {"$inroom[cname]" != "$inroom[map][name]"} {
		#local __color {<139>};
	};
	#format {result} {%s - %s%s<099> #%s@%s} {$inroom[cname]} {$__color} {$inroom[map][name]} {$inroom[map][vnum]} {$inroom[map][area]}
}
#alias {g} {#if {"%1" == ""} {#echo {Syntax: g <room name>};#return};#map run {%1} {0.5}}
#alias {map_pos} {
	#if {"%1" == ""} {#echo {Syntax: map_pos <room name>|<room evnum>|<exits>};#return};
	#map goto {%1};
}

#alias {scout} {
	#if {"%2" == ""} {#echo {Syntax: scout <keyword> <area> [<who>]};#return};
	#if {$scout[run] == 1} {#echo {You are still in scouting...};#bell;#return};
	
	#var scout {{run}{1}{mstep}{0}{rstep}{0}{gstep}{0}{target}{%1}{area}{%2}{notify}{%3}{rvnum}{0}{rtitle}{}{rooms}{}{index}{1}};
    #map list {roomarea} {%2} {variable} {scout[rooms]};
    #if {&scout[rooms][] == 0} {ui_message {No room in area %2};#bell;#var scout[run] 0;#return};
	#if {*scout[rooms][+$scout[index]] == $inroom[map][vnum]} {#math scout[index] {$scout[index] + 1}};
    #act {$scout[target]} {
		#var scout[rvnum] $inroom[map][vnum];
		#var scout[rtitle] $inroom[map][name];
		#bell;
		scout_stop;
	} {1};
	
    scout_next;
	#math scout[mstep] {$scout[mstep] + 1};
	#path walk forward;
	#event {END OF PATH} {
        #math scout[index] {$scout[index] + 1};
        #if {$scout[index] > &scout[rooms][]} {
            scout_stop;
			#return;
        };
		scout_next;
    };
}
#alias {scout_next} {
	#local next {};
	#map get roomname {next} *scout[rooms][+$scout[index]];
	#map find $next;
}
#alias {scout_stop} {
	#if {$scout[run] != 1} {#echo {The scouting is already stop};#return};
	#var scout[run] 0;
	#path destroy;
	#unact {$scout[target]};
	#unevent {END OF PATH};
	#if {"$scout[rtitle]" == ""} {#local s {[scouter]: $scout[target] not in $scout[area]}};
	#else {#local s {[scouter]: $scout[target] is in $scout[rtitle]($scout[rvnum]) of $scout[area]}};
	ui_message {$s};
	#if {"$scout[notify]" != ""} {tell $scout[notify] $s};
}
#func {map_area_c2e} {
	#switch {"%0"} {
		#case "长安城" {#return changan};
		#case "长安城西" {#return changan-west};
		#case "开封城" {#return kaifeng};
		#case "高老庄" {#return gao};
		#case "方寸山" {#return fangcun};
		#case "龙宫" {#return longgong};
		#case "普陀山" {#return putuo};
		#default {#return %0};
	}
}
#nop ####### Edit ######
#var edit {{name}{0}}
#alias {map.new} {#map dig %0 new}
#alias {map.autoname} {#math edit[name] {($edit[name] + 1) % 2};#local s {ON};#if {$edit[name] == 0} {#local s {OFF}};ui_message {Switch auto-name to $s};#bell};
#alias {map.setname} {
	#map set roomname $inroom[cname];
	#var inroom[map][name] $inroom[cname];
	#bell;
}
#alias {map.save} {
	#map write {./map/xyj.map};
	#echo {Saved map to ./map/xyj.map};
}
#alias {map.check} {
    #map list {variable} {__rooms};
    #local {__names} {};
    #local cnt 0;
	#local __id 0;
	#foreach {*__rooms[%*]} {__id} {
		#map get roomname {__name} {$__id};
		#if {"$__name" == ""} {#echo {#$__id was empty name};#continue};
		#if {$__names[$__name][count] >= 1} {
            #if {$__names[$__name][count] == 1} {#map set roomname {$__name$__names[$__name][count]} {$__names[$__name][id]};#math cnt {$cnt+1}};
            #math {__names[$__name][count]} {$__names[$__name][count]+1};
            #map set roomname {$__name$__names[$__name][count]} {$__id};
            #math cnt {$cnt+1};
        };
		#else {
            #local __names[$__name] {{id}{$__id}{count}{1}};
		}
	};
	#unvar __rooms;
	#unvar __name;
    #echo {$cnt duplicated names has been changed.};
}
#alias fly {
	#send {fly %0};
	#act {^到了！你按下云头跳了下来。$} {map_pos %0;#unact {^到了！你按下云头跳了下来。$}};
	#act {^咦？．．．怎么还在原来的地方？$} {#map undo;#unact {^到了！你按下云头跳了下来。$}};
	#delay {3} {#unact {^到了！你按下云头跳了下来。$}};
}
#act {^警幻仙姑说道：又是一根木头...好，你这就去吧。$} {map_pos $bed;#unvar bed}
#alias sleep {#var bed $inroom[map][vnum];#send sleep %0}
#act {^咦？．．．怎么还在原来的地方？$} {#map undo;#unact {^到了！你按下云头跳了下来。$}};
#act {^你的动作还没有完成，不能移动。$} {#map undo}
